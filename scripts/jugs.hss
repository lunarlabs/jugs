#   # # # ### ###
#   # # # #   #  
#   # # # # # ###
# # # # # # #   #
# ### ### ### ###
#
# ichiro 2022.

#50 ms/frame

include, utilities.lib #Useful crap

plotscript, start the title, begin
	set tag(tag:saves available, save system check)
	variable(container, sli, startmenu, i)
	suspend player
	stop song
	container := load slice collection(1)
	
	wait and listen(150)
	
	fade screen out
	load palette(0)
	play song(song:kcdancinonamiga)
	for(i,1,3,1) do(
		sli := first child(first child(container))
		free slice(sli)
	)
	
	sli := create text
	set parent(sli, first child(container))
	
	set horiz align (sli, edge:center)
	set horiz anchor(sli, edge:center)
	set vert align (sli, edge:middle)
	set vert anchor(sli, edge:middle)
	
	$1 = "Ichiro proudly presents"
	set slice text(sli,1)
	wait(1)
	fade screen in
	wait and listen(100)
	fade screen out
	
	$1 = "a game for \nthe OHRRPGCE"
	set slice text(sli,1)
	wait(1)
	fade screen in
	wait and listen(100)
	fade screen out
	
	$1 = "based on a true story\nthat I kind of made up"
	set slice text(sli,1)
	wait(1)
	fade screen in
	wait and listen(100)
	
	sli := lookup slice(sli:TitleSprite)
	set slice visible(sli, on)
	dissolve sprite(sli, dissolve:fade, 20, 0, true)
	wait for dissolve(sli)
	wait(5)
	set sprite set number(sli, 2)
	wait(1)
	set sprite set number(sli, 3)
	wait(1)
	set sprite set number(sli, 4)
	play sound(sfx:Metal_Hit)
	wait for sound(sfx:Metal_Hit)
	play sound(sfx:Awcrap2)
	wait for sound(sfx:Awcrap2)
	wait(25)
	sli := lookup slice(sli:PressPlay)
	set slice visible(sli, on)
	wait for key(use key)
	set slice visible(sli, off)
	play sound(sfx:UIFX4)
	startmenu := open menu (menu:start, off)
end

plotscript, go load from main menu, begin
	variable(sli, slot)
	play sound(sfx:UIFX4)
	fade screen out
	reset palette
	sli := lookup slice(sli:TitleSprite)
	set slice visible(sli, off)
	sli := first text child(previous sibling(sli))
	set slice visible(sli, off)
	wait(1)
	fade screen in
	slot := load menu (false, false)
	if (slot > 0) then (load from slot(slot))
	else (reset game)
end

plotscript, end of line, begin
	stop song
	variable(container,mnu,sli1,sli2)
	container := load slice collection(3)
	sli1 := first child(container) # the floating head
	sli2 := next sibling(sli1) # the text
	
	mnu := open menu(menu:Game Over,off)
	
	set slice visible(sli1, on)
	play sound(sfx:TornadoMagic)
	dissolve sprite(sli1, dissolve:ghostly fade, 30, 0, true)
	wait for dissolve(sli1)
	play sound(sfx:butzlaugh)
	evil head loop
	
	subscript, evil head loop, begin
	while(true) do(
		move slice to(sli1, 4, -21, 15)
		wait for slice(sli1)
		wait(3)
		move slice to(sli1, 4, -11, 15)
		wait for slice(sli1)
		wait(3)
		move slice to(sli1, 4, -21, 15)
		wait for slice(sli1)
		wait(3)
		move slice to(sli1, 4, -11, 15)
		wait for slice(sli1)
		wait(3)
	)
end
	
end



include, story.hss